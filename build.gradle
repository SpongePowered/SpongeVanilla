// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'gradle.plugin.org.spongepowered:spongegradle:0.8.1'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
    }
}

// Apply shared implementation Gradle config
apply from: project(':SpongeCommon').file('gradle/implementation.gradle')

version = "$minecraft.version-$apiSuffix-$buildNumber"

minecraft {
    tweakClass = 'org.spongepowered.server.launch.VanillaServerTweaker'

    atSource sourceSets.main
}
ats += 'vanilla_at.cfg'

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
}

configurations.forgeGradleMcDeps {
    transitive = false
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.2'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.3'

    compile 'org.jline:jline-reader:3.3.0'
    compile 'org.jline:jline-terminal-jna:3.3.0'
    compile 'net.java.dev.jna:jna:4.4.0'

    shadow 'org.ow2.asm:asm-all:5.2'
}

// Set main class and classpath for Minecraft server and Launchwrapper
jar {
    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.server.launch.VersionCheckingMain',
                'Class-Path': 'minecraft_server.1.11.2.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar'
        )
    }
}

// Reobfuscate to SRG mappings
reobf.jar.mappingType = 'SEARGE'

shadowJar {
    // Include production log4j2.xml
    exclude 'log4j2.xml'
    rename 'log4j2_prod.xml', 'log4j2.xml'

    configurations += [project.configurations.shadow]

    from plugins[forgeGradlePlugin].delayedFile(net.minecraftforge.gradle.common.Constants.MCP_DATA_SRG)
    rename 'joined.srg', 'mappings.srg'

    dependencies {
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        include dependency('com.typesafe:config')

        include dependency('org.jline:jline-terminal')
        include dependency('org.jline:jline-terminal-jna')
        include dependency('org.jline:jline-reader')
        include dependency('net.java.dev.jna:jna')
    }
}

license {
    exclude 'net/minecraftforge/**'
}
