// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'gradle.plugin.org.spongepowered:spongegradle:0.8.1'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'de.sebastianboegl.gradle.plugins:shadow-log4j-transformer:1.0.1'
    }
}

// Apply shared implementation Gradle config
apply from: project(':SpongeCommon').file('gradle/implementation.gradle')
apply plugin: 'de.sebastianboegl.shadow.transformer.log4j'

version = "$minecraft.version-$apiSuffix-$buildNumber"

minecraft {
    tweakClass = 'org.spongepowered.server.launch.VanillaServerTweaker'

    atSource sourceSets.main
}
ats += 'vanilla_at.cfg'

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
    log4j
}

configurations.forgeGradleMcDeps {
    transitive = false
}

repositories {
    maven {
        name = 'sonatype-oss-snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.2'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.3'

    compile 'org.apache.logging.log4j:log4j-iostreams:2.8.1'

    // Terminal console
    compile 'net.minecrell:terminalconsoleappender:1.0.0-SNAPSHOT'
    compile 'org.jline:jline-terminal-jna:3.3.1'
    compile 'net.java.dev.jna:jna:4.4.0'

    shadow 'org.ow2.asm:asm-all:5.2'

    log4j('org.apache.logging.log4j:log4j-core:2.8.1') {
        transitive = false
    }
}

// Set main class and classpath for Minecraft server and Launchwrapper
jar {
    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.server.launch.VersionCheckingMain',
                'Class-Path': 'minecraft_server.1.12.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar'
        )
    }
}

// Reobfuscate to SRG mappings
reobf.jar.mappingType = 'SEARGE'

shadowJar {
    // Include production log4j2.xml
    exclude 'log4j2.xml'
    rename 'log4j2_prod.xml', 'log4j2.xml'

    configurations += [project.configurations.shadow]

    from plugins[forgeGradlePlugin].delayedFile(net.minecraftforge.gradle.common.Constants.MCP_DATA_SRG)
    rename 'joined.srg', 'mappings.srg'

    from({zipTree(project.configurations.log4j.resolve().first())}) {
        include 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
    }

    dependencies {
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        include dependency('com.typesafe:config')

        include dependency('org.apache.logging.log4j:log4j-iostreams')

        include dependency('net.minecrell:terminalconsoleappender')
        include dependency('org.jline:jline-terminal')
        include dependency('org.jline:jline-terminal-jna')
        include dependency('org.jline:jline-reader')
        include dependency('net.java.dev.jna:jna')
    }
}

license {
    exclude 'net/minecraftforge/**'
}
