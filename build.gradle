// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava'
        }
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'org.spongepowered:spongegradle:0.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-server'
apply plugin: 'org.spongepowered.mixin'

ext.minecraftPlugin = plugins.getPlugin('net.minecraftforge.gradle.tweaker-server')
ext.DIR_MCP_MAPPINGS = net.minecraftforge.gradle.common.Constants.DIR_MCP_MAPPINGS

// Apply shared implementation Gradle config
apply from: project(':SpongeCommon').file('gradle/implementation.gradle')

minecraft {
    version = '1.8.9'
    tweakClass = 'org.spongepowered.server.launch.VanillaServerTweaker'
}

version = "$minecraft.version-$apiVersion-$buildNumber"

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.0.4'
    compile 'jline:jline:2.13'

    shadow 'org.ow2.asm:asm-all:5.0.4'
}

// Set main class and classpath for Minecraft server and Launchwrapper
jar {
    manifest {
        attributes(
                'Main-Class': 'org.spongepowered.server.launch.VersionCheckingMain',
                'Class-Path': 'minecraft_server.1.8.9.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar'
        )
    }
}

ats += 'vanilla_at.cfg'

ext.shadowConfig = {
    // Include production log4j2.xml
    rename 'log4j2_prod.xml', 'log4j2.xml'

    configurations += [project.configurations.shadow]

    from minecraftPlugin.delayedFile(net.minecraftforge.gradle.common.Constants.MCP_DATA_SRG)
    rename 'joined.srg', 'mappings.srg'

    dependencies {
        include dependency('net.sf.trove4j:trove4j')

        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        include dependency('com.typesafe:config')

        include dependency('jline:jline')
    }
}
shadowJar shadowConfig
shadowDeobfJar shadowConfig
